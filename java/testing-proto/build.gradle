buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.19'
    }
}

apply plugin: 'java-library'
apply plugin: 'com.google.protobuf'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        implementation 'javax.annotation:javax.annotation-api:1.3.2'
    }

    implementation "com.google.protobuf:protobuf-java:${gradle.ext.projectProtobufVersion}"
    implementation "io.grpc:grpc-protobuf:${gradle.ext.projectGrpcVersion}"
    implementation "io.grpc:grpc-stub:${gradle.ext.projectGrpcVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${gradle.ext.projectProtobufVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${gradle.ext.projectGrpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives sourcesJar, javadocJar
}
